/*
 * Copyright 2000-2018 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
}

plugins {
    id "com.github.jk1.tcdeps" version "0.15"
}

repositories {
    teamcityServer {
        url = 'http://teamcity.jetbrains.com'
    }
    jcenter()

}

configurations {
    kotlinJVM
    kotlinJS
    library
}

dependencies {
    compile project(":kotlin.web.demo.backend:compilers")

    def root = "$kotlinBuildType:$kotlinBuild:kotlin-plugin-${pluginBuild}.zip!/Kotlin/kotlinc/lib"

    kotlinJVM tc("$root/kotlin-compiler.jar")
    kotlinJVM tc("$root/kotlin-script-runtime.jar")
    kotlinJVM tc("$root/kotlin-runtime.jar")
    kotlinJVM tc("$kotlinBuildType:$kotlinBuild:kotlin-plugin-${pluginBuild}.zip!/Kotlin/lib/kotlin-plugin.jar")
    kotlinJS tc("$root/kotlin-jslib.jar")

    library tc("$root/kotlin-reflect.jar")
    library tc("$root/kotlin-test.jar")
    library group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: stdlibVersion
    library "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.1"

    /**
     * ADD DEPENDENCIES TO KOTLIN 1.2.41 COMPILER
     *
     * If you want to add some another dependencies to kotlin-compiler you can use 'library' task.
     *
     * For example:
     *
     * library "your dependency from maven"
     *
     * FYI: Pay attention if yor library has got reflections, work with files and etc
     * Please configure the 'executors.policy.template' in web-demo-backend.
     * @see <a href="https://docs.oracle.com/javase/7/docs/technotes/guides/security/PolicyFiles.html">Java Security Police</a>
     *
     * HOW TO SET Java Security Police in 'executors.policy.template'
     *
     * If you want to add own dependency please use marker @WRAPPERS_LIB@
     *
     * For example:
     *
     * grant codeBase "file:@WRAPPERS_LIB@/junit-4.12.jar" {
     *      permission java.lang.reflect.ReflectPermission "suppressAccessChecks";
     *      permission java.lang.RuntimePermission "setIO";
     *      permission java.io.FilePermission "<<ALL FILES>>", "read";
     *      permission java.lang.RuntimePermission "accessDeclaredMembers";
     * };
     */
     
    library "com.beust:klaxon:3.0.1"
    library 'com.github.kittinunf.fuel:fuel:1.13.0' //for JVM

    compile fileTree(dir: projectDir.toString() + File.separator + "kotlin", include: '*.jar')
}

task copyKotlinLibs(type: Copy) {
    from configurations.kotlinJVM
    into projectDir.toString() + File.separator + "kotlin"
}

